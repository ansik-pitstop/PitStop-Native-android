apply plugin: 'com.android.application'
apply plugin: 'android-apt'

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
    }
}

android {
    compileOptions.encoding = 'ISO-8859-1'
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    def staging = "\"http://staging.api.getpitstop.io:10010/\""
    def snapshot = "\"http://snapshot.api.getpitstop.io:10011/\""
    def testing = "\"http://45.55.82.111:10011/\""
    def isStaging = true

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent,
                    file.name.replace(".apk", "_${variant.versionName}.apk").replace("app-", "Pitstop_"))
        }
    }
    defaultConfig {
        applicationId "com.ansik.pitstop"
        minSdkVersion 18
        targetSdkVersion 24
        // Change versioning in productFlavors instead of here
        versionCode 98
        versionName "2.1.17"
        multiDexEnabled = true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        debug { // for development, dev backend, no mixpanel
            buildConfigField "String", "CLIENT_ID", "\"DINCPNWtqjjG69xfMWuF8BIJ8QjwjyLwCq36C19CkTIMkFnE6zSxz7Xoow0aeq8M6Tlkybu8gd4sDIKD\""
            buildConfigField "String", "SERVER_URL", isStaging ? staging : snapshot
            buildConfigField "String", "ENDPOINT_TYPE", isStaging ? "\"staging\"" : "\"snapshot\""
            versionNameSuffix ".debug"
        }
        beta.initWith(buildTypes.debug)
        beta { // for release to beta in Play Store, dev backend, mixpanel
            debuggable false
            versionNameSuffix ".beta"
        }
        release { // for release to production, prod backend, mixpanel
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CLIENT_ID", "\"8pLSeDGJdlW89dzfbfI4uNr0x2Blo4FG8TEkCB5LXKgtfVf8\""
            buildConfigField "String", "SERVER_URL", "\"https://api.getpitstop.io/\""
            buildConfigField "String", "ENDPOINT_TYPE", "\"release\""
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    productFlavors {
        pitstop {
            versionCode 116
            versionName "2.1.27"
            buildConfigField "String", "BETA_TYPE", "\"beta\""
            buildConfigField "String", "DEBUG_TYPE", "\"debug\""
            buildConfigField "String", "RELEASE_TYPE", "\"release\""
        }
        dealerTest {
            versionCode 3
            versionName "1.0.2"
            applicationIdSuffix ".test_app"
            buildConfigField "String", "TEST_CLIENT_ID", "\"IhUvDTvmbBgMFARwqVuPYrMIK8XVkGHoRAH2YsQUcikMDn3F\""
            buildConfigField "String", "TEST_SERVER_URL", "\"http://52.204.33.174:8081/\""
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Multidex support library - Use when Building Apps with Over 65K Methods
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.goebl:david-webb:1.3.0'
//    compile 'io.reactivex:rxjava:1.1.6'
//    compile 'io.reactivex:rxandroid:1.2.1'
    pitstopCompile 'com.google.android.gms:play-services-vision:9.4.0'
    pitstopCompile 'com.google.android.gms:play-services-gcm:9.4.0'
    pitstopCompile 'com.parse:parse-android:1.+'
    pitstopCompile 'com.facebook.android:facebook-android-sdk:4.12.1'
    pitstopCompile 'io.smooch:core:latest.release'
    pitstopCompile 'io.smooch:ui:latest.release'
    pitstopCompile 'com.mixpanel.android:mixpanel-android:4.8.0'
    pitstopCompile 'com.journeyapps:zxing-android-embedded:3.3.0@aar'
    pitstopCompile 'com.google.zxing:core:3.2.1'
    pitstopCompile project(":libs:MaterialShowcaseView")
    pitstopCompile 'com.github.bmarrdev:android-DecoView-charting:v0.9.6'
    pitstopCompile 'com.github.brnunes:swipeablerecyclerview:1.0.1'
    dealerTestCompile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
}

apply plugin: 'com.google.gms.google-services'